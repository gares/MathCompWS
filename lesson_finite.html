<!DOCTYPE html>
<!--

README:
  - coqdoc_ids must contain all text-area with Coq code
  - init_pkgs may contain ['init, 'math-comp'] for algebra

  These variables are signalled by a "HERE" in a comment

-->
<html xmlns="http://www.w3.org/1999/xhtml">
  <head>
    <meta http-equiv="content-type" content="text/html;charset=utf-8" />

    <link rel="stylesheet" href="jscoq/external/bootstrap.min.css" />

    <title>Lesson 3 : Finite types</title>
  </head>


<body>
  <div id="ide-wrapper" class="toggled">
  <div id="code-wrapper">
  <div id="document">

<!-- lesson -->

<h1>lesson_finite</h1>
<div><textarea id='coq-ta-1'>
</textarea></div>
<div>
To play this document inside your browser use ALT-N and ALT-P.
<hr/>

<h2>
 Objective of this course
</h2>

  Understand the benefits and usage of finite types.
<ul class="doclist">
  <li> formation principles

  </li>
<li> new tools: finite functions and finite set theory

</li>
<li> A special case : ordinals
<hr/>

<h2>
 Formation principle
</h2>


</li>
<li> For a finite type, you can enumerate the elements
    the enumeration is a simple piece of data (based on sequences)

</li>
<li> The enumeration gives some computation principle
<hr/>

</div>
<div><textarea id='coq-ta-2'>
From mathcomp Require Import all_ssreflect.

</textarea></div>
<div>
the simplest finite types: ordinal numbers.

</li>
<li> initial segments of natural numbers

</li>
<li> can be used as building blocks or yardsticks for other finite types. </div>
<div><textarea id='coq-ta-3'>

About Ordinal.

Example two_in_I_4 := Ordinal (isT : 2 < 4).

Fail Example five_in_I_4 : Ordinal (isT : 5 < 4).

(* For ordinal types that contain at least one elements, there
  is an optimistic injection from nat. *)

Check inord.

Check inordK.

Example inord_val_3_4 : inord 2 = two_in_I_4 :> 'I_4.
Proof.
rewrite -[X in inord X]/(nat_of_ord two_in_I_4).
rewrite inord_val. by [].
Qed.

(*
  - In the rest of this talk I will use the following pattern to
    verify that I have a finite type.
*)

Check [finType of 'I_4].

Fail Check [finType of nat].

</textarea></div>
<div>
Finite type constructions.

</li>
<li> New finite types can be built from existing ones
    ordinal types are the usual examples of finite types.
    Cartesian product, Disjoint sum, subtype, function type.

</li>
</ul>
</div>
<div><textarea id='coq-ta-4'>

Check [finType of 'I_4 * 'I_3].

Definition twin_primes_lt100 :=
 {x : 'I_100 * 'I_100 | prime (fst x) &&
                      prime (snd x) && (snd x == (fst x) + 2 :> nat)}.

Check [finType of twin_primes_lt100].

Definition lesser_twin_100 :=
 {x : nat | prime x && prime (x + 2) && (x < 100)}.

Fail Check [finType of lesser_twin_100].

(* There is a short solution to make lesser_twin_100 a finite type,
   but it is not part of this lesson. *)

Definition lesser_twin_100_in_ord (x : lesser_twin_100) : 'I_100.
case : x => x /andP [_ px]; exact (Ordinal px).
Defined.

Lemma build_andb (a b : bool) : a -> b -> a && b.
by move => -> ->. Qed.

Definition lesser_twin_from_ord (x : 'I_100) : option lesser_twin_100 :=
  match x with
  | Ordinal x px => 
     match Sumbool.sumbool_of_bool (prime x && prime (x + 2)) with
     | left h => Some (exist _ x (build_andb _ _ h px))
     | right h => None
     end
  end.

Lemma Pcan100 : pcancel lesser_twin_100_in_ord lesser_twin_from_ord.
Proof. case => x p /=.
case: (elimTF andP p) => p1 p2.
rewrite /lesser_twin_from_ord.
case: (Sumbool.sumbool_of_bool (prime x && prime (x + 2))).
  by move => a; congr Some; apply: val_inj => /=.
by rewrite p1.
Qed.

Module LessonSandBox1.

</textarea></div>
<div>
An example: building one's own finite type. Show that it
   is finite by exhibiting an injection into a finite type. </div>
<div><textarea id='coq-ta-5'>

Inductive card_point : predArgType := W | E | N | S.

Definition cp2o d : 'I_4 :=
  match d with
    W => inord 0 | E => inord 1 | N => inord 2 | S => inord 3
  end.

Definition o2cp (n : 'I_4) :=
  match val n with
    0 => Some W | 1 => Some E | 2 => Some N | 3 => Some S | _ => None
  end.

Lemma cp_can : pcancel cp2o o2cp.
Proof. by case; rewrite /o2cp /= inordK. Qed.

</textarea></div>
<div>
The lemma cp_can means that there is an injection from
  card_point into a known finite type.  In one shot, this
  gives us that card_point equality is decidable in card_point,
  that there is a canonical way to construct witness for every
  satisfiable predicate, that the elements can be enumerated,
  and that the type card_point is finite. </div>
<div><textarea id='coq-ta-6'>

Canonical cp_eqType := EqType card_point (PcanEqMixin cp_can).
Canonical card_point_choiceType :=
    ChoiceType card_point (PcanChoiceMixin cp_can).
Canonical cp_countType :=
    CountType card_point (PcanCountMixin cp_can).
Canonical card_point_finType := FinType card_point (PcanFinMixin cp_can).

Check [finType of card_point].

End LessonSandBox1.

</textarea></div>
<div>
Tools about finiteness.
<ul class="doclist">
  <li> Quantifications of decidable predicates become decidable. </div>
  <div><textarea id='coq-ta-7'>
  
Check [forall x : 'I_4, x < 2] && [exists x :'I_4, 4 < x].

Check [forall x : {x : 'I_100 | prime x}, 1 < val x].

(* talk about connect, as predicate on graph. *)
</textarea></div>
</div> <!-- /#document -->
</div> <!-- /#code-wrapper -->
</div> <!-- /#ide-wrapper  -->

<script src="./jscoq/js/jscoq-loader.js" type="text/javascript"></script>
<script type="text/javascript">


   var coqdoc_ids = ['coq-ta-1',  'coq-ta-2',  'coq-ta-3',  'coq-ta-4', 
                    'coq-ta-5',  'coq-ta-6',  'coq-ta-7'];
    var coq;

    loadJsCoq('./jscoq/')
              .then(loadJs("./jscoq/external/CodeMirror/addon/runmode/runmode"))
              .then(loadJs("./jscoq/external/CodeMirror/addon/runmode/colorize"))
              .then( function () {
                  var coqInline = document.getElementsByClassName("inline-coq");
                  CodeMirror.colorize(coqInline); })
              .then( function () {
                  coq = new CoqManager (coqdoc_ids,
                                       {
			base_path: './jscoq/',
		        init_pkgs: ['init']    // HERE
		}
                  );
              });
  </script>
</body>
</html>
